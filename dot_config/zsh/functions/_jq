#compdef jq

# Written by parsing (manually) `jq --help` on jq 1.8.1.

local context state state_descr line
typeset -A opt_args

_arguments -s -S \
  '(-n --null-input)'{-n,--null-input}'[use null as the single input value]' \
  '(-R --raw-input)'{-R,--raw-input}'[read each line as string instead of JSON]' \
  '(-s --slurp)'{-s,--slurp}'[read all inputs into an array]' \
  '(-c --compact-output)'{-c,--compact-output}'[compact instead of pretty-printed output]' \
  '(-r --raw-output)'{-r,--raw-output}'[output strings without escapes and quotes]' \
  --raw-output0'[implies -r and output NUL after each output]' \
  '(-j --join-output)'{-j,--join-output}'[implies -r and output without newline]' \
  '(-a --ascii-output)'{-a,--ascii-output}'[output strings by only ASCII characters]' \
  '(-S --sort-keys)'{-S,--sort-keys}'[sort keys of each object on output]' \
  '(-C --color-output)'{-C,--color-output}'[colorize JSON output]' \
  '(-M --monochrome-output)'{-M,--monochrome-output}'[disable colored output]' \
  --tab'[use tabs for indentation]' \
  '--indent[use n spaces for indentation]:spaces' \
  --unbuffered'[flush output stream after each output]' \
  --stream'[parse the input value in streaming fashion]' \
  --stream-errors'[implies --stream and report parse error as an array]' \
  --seq'[parse input/output as application/json-seq]' \
  '(-f --from-file)'{-f,--from-file}'[load the filter from a file]:filter file:_files' \
  '(-L --library-path)'{-L,--library-path}'[search modules from the directory]:library directory:_files -/' \
  '*--arg[set variable to string value]:variable name: :value:' \
  '*--argjson[set variable to JSON value]:variable name: :JSON value:' \
  '*--slurpfile[set variable to JSON array from file]:variable name: :file:_files' \
  '*--rawfile[set variable to string content of file]:variable name: :file:_files' \
  '(-e --exit-status)'{-e,--exit-status}'[set exit status code based on the output]' \
  '(-V --version)'{-V,--version}'[show the version]' \
  --build-configuration"[show jq's build configuration]" \
  '(-h --help)'{-h,--help}'[show the help]' \
  '(: -)--args[consume remaining arguments as positional string values]:*:args' \
  '(: -)--jsonargs[consume remaining arguments as positional JSON values]:*:json args' \
  '(-f --from-file)1:jq filter: ' \
  '*:input file:_files'
